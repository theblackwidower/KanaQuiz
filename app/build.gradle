apply plugin: 'com.android.application'
//ref: https://stackoverflow.com/a/49739351/3582371
if (getGradle().getStartParameter().getTaskNames().toString().toLowerCase().contains('release')) {
    apply plugin: 'com.google.firebase.firebase-perf'
    apply plugin: 'com.google.firebase.crashlytics'
}

android {
    compileSdkVersion 33
    buildToolsVersion '33.0.0'
    defaultConfig {
        applicationId 'com.noprestige.kanaquiz'
        minSdkVersion 22
        targetSdkVersion 33
        versionCode 19
        versionName '0.15'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        //ref: https://stackoverflow.com/a/47525966/3582371
        resConfigs 'en-rCA', 'es-rES', 'ca-rES', 'de-rDE'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['room.schemaLocation':
                                     "$projectDir/schemas".toString()]
            }
        }
        // Required when setting minSdkVersion to 20 or lower
        multiDexEnabled true
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            applicationIdSuffix '.dev'
            def gitBranchName = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim().replace('-', '_')
            //ref: https://stackoverflow.com/q/19172565
            versionNameSuffix '-' + gitBranchName + '-' + new Date().format('yyyyMMddHHmm')
            debuggable true
        }
        pure {
            initWith release
            //TODO: Get this working.
            //manifestPlaceholders = [mainClassName:'.KanaQuizPure']
            versionNameSuffix '-pure'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    namespace 'com.noprestige.kanaquiz'
    //ref: planet.jboss.org/post/android_lint_ignore_specific_errors
    lint {
        disable 'MissingTranslation'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.jetbrains:annotations:24.0.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    implementation 'com.google.android.material:material:1.11.0'

    implementation 'androidx.room:room-runtime:2.6.1'
    annotationProcessor 'androidx.room:room-compiler:2.6.1'

    androidTestImplementation 'androidx.room:room-testing:2.6.1'

    releaseImplementation 'com.google.firebase:firebase-core:21.1.1'
    releaseImplementation 'com.google.firebase:firebase-perf:20.5.2'
    releaseImplementation 'com.google.firebase:firebase-crashlytics:18.6.2'
    releaseImplementation 'com.google.firebase:firebase-analytics:21.5.1'

    implementation 'org.apmem.tools:layouts:1.10@aar'

    implementation 'com.jjoe64:graphview:4.2.2'

    pureImplementation 'ch.acra:acra-mail:5.10.1'
    pureImplementation 'ch.acra:acra-toast:5.10.1'
    pureImplementation 'ch.acra:acra-limiter:5.10.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'

    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'

    implementation 'androidx.preference:preference:1.2.1'

    implementation 'moe.shizuku.fontprovider:api:10'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
}

coveralls {
    sourceDirs += "${rootDir}/app/src/main/java/"
    //Unit tests
    jacocoReportPath "${buildDir}/reports/jacoco/debug/jacoco.xml"
    //Instrument tests
    //jacocoReportPath "${buildDir}/reports/coverage/debug/report.xml"
}

//ref: https://stackoverflow.com/a/49739351/3582371
if (getGradle().getStartParameter().getTaskNames().toString().toLowerCase().contains('release')) {
    apply plugin: 'com.google.gms.google-services'
}
