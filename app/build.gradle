apply plugin: 'com.android.application'
//ref: https://stackoverflow.com/a/49739351/3582371
if (getGradle().getStartParameter().getTaskNames().toString().toLowerCase().contains('release')) {
    apply plugin: 'com.google.firebase.firebase-perf'
    apply plugin: 'com.google.firebase.crashlytics'
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    defaultConfig {
        applicationId 'com.noprestige.kanaquiz'
        minSdkVersion 17
        targetSdkVersion 30
        versionCode 18
        versionName '0.14.1'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        //ref: https://stackoverflow.com/a/47525966/3582371
        resConfigs 'en-rCA', 'es-rES', 'ca-rES', 'de-rDE'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['room.schemaLocation':
                                     "$projectDir/schemas".toString()]
            }
        }
        // Required when setting minSdkVersion to 20 or lower
        multiDexEnabled true
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            applicationIdSuffix '.dev'
            def gitBranchName = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim().replace('-', '_')
            //ref: https://stackoverflow.com/q/19172565
            versionNameSuffix '-' + gitBranchName + '-' + new Date().format('yyyyMMddHHmm')
            debuggable true
        }
        pure {
            initWith release
            //TODO: Get this working.
            //manifestPlaceholders = [mainClassName:'.KanaQuizPure']
            versionNameSuffix '-pure'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //ref: planet.jboss.org/post/android_lint_ignore_specific_errors
    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.jetbrains:annotations:15.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    testImplementation 'junit:junit:4.13.2'
    implementation 'com.google.android.material:material:1.3.0'

    implementation 'androidx.room:room-runtime:2.3.0'
    annotationProcessor 'androidx.room:room-compiler:2.3.0'

    androidTestImplementation 'androidx.room:room-testing:2.3.0'

    releaseImplementation 'com.google.firebase:firebase-core:19.0.0'
    releaseImplementation 'com.google.firebase:firebase-perf:20.0.0'
    releaseImplementation 'com.google.firebase:firebase-crashlytics:18.0.0'
    releaseImplementation 'com.google.firebase:firebase-analytics:19.0.0'

    implementation 'org.apmem.tools:layouts:1.10@aar'

    implementation 'com.jjoe64:graphview:4.2.2'

    pureImplementation 'ch.acra:acra-mail:5.8.1'
    pureImplementation 'ch.acra:acra-toast:5.8.1'
    pureImplementation 'ch.acra:acra-limiter:5.8.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'

    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'

    implementation 'androidx.preference:preference:1.1.1'

    implementation 'moe.shizuku.fontprovider:api:10'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}

coveralls {
    sourceDirs += "${rootDir}/app/src/main/java/"
    //Unit tests
    jacocoReportPath "${buildDir}/reports/jacoco/debug/jacoco.xml"
    //Instrument tests
    //jacocoReportPath "${buildDir}/reports/coverage/debug/report.xml"
}

//ref: https://stackoverflow.com/a/49739351/3582371
if (getGradle().getStartParameter().getTaskNames().toString().toLowerCase().contains('release')) {
    apply plugin: 'com.google.gms.google-services'
}
