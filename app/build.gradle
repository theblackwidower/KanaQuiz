apply plugin: 'com.android.application'
//ref: https://stackoverflow.com/a/49739351/3582371
if (getGradle().getStartParameter().getTaskNames().toString().toLowerCase().contains('release')) {
    apply plugin: 'io.fabric'
    apply plugin: 'com.google.firebase.firebase-perf'
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId 'com.noprestige.kanaquiz'
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 18
        versionName '0.14.1'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        //ref: https://stackoverflow.com/a/47525966/3582371
        resConfigs 'en-rCA', 'es-rES', 'ca-rES', 'de-rDE'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['room.schemaLocation':
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            applicationIdSuffix '.dev'
            def gitBranchName = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim().replace('-', '_')
            //ref: https://stackoverflow.com/q/19172565
            versionNameSuffix '-' + gitBranchName + '-' + new Date().format('yyyyMMddHHmm')
            debuggable true
        }
        pure {
            initWith release
            //TODO: Get this working.
            //manifestPlaceholders = [mainClassName:'.KanaQuizPure']
            versionNameSuffix '-pure'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //ref: planet.jboss.org/post/android_lint_ignore_specific_errors
    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    testImplementation 'junit:junit:4.12'
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'androidx.room:room-runtime:2.1.0'
    annotationProcessor 'androidx.room:room-compiler:2.1.0'

    androidTestImplementation 'androidx.room:room-testing:2.1.0'

    releaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    releaseImplementation 'com.google.firebase:firebase-core:17.0.1'
    releaseImplementation 'com.google.firebase:firebase-perf:18.0.1'

    implementation 'org.apmem.tools:layouts:1.10@aar'

    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'

    implementation 'com.jjoe64:graphview:4.2.2'

    pureImplementation 'ch.acra:acra-mail:5.4.0'
    pureImplementation 'ch.acra:acra-toast:5.4.0'
    pureImplementation 'ch.acra:acra-limiter:5.4.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'

    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'

    implementation 'androidx.preference:preference:1.0.0'

    implementation 'moe.shizuku.fontprovider:api:10'
}

coveralls {
    sourceDirs += "${rootDir}/app/src/main/java/"
    //Unit tests
    jacocoReportPath "${buildDir}/reports/jacoco/debug/jacoco.xml"
    //Instrument tests
    //jacocoReportPath "${buildDir}/reports/coverage/debug/report.xml"
}

//ref: https://stackoverflow.com/a/49739351/3582371
if (getGradle().getStartParameter().getTaskNames().toString().toLowerCase().contains('release')) {
    apply plugin: 'com.google.gms.google-services'
}
